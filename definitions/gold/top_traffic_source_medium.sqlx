config {
type: "table",
schema: "dataform",
name: "gold_top_traffic_source_medium",
description: "Monthly KPIs per traffic_source.medium: purchased value USD, total items, avg items per purchase"
}


WITH silver AS (
SELECT * FROM ${ref("silver_purchase_traffic_source_medium")}
),
items_resolved AS (
SELECT
event_date,
traffic_source_medium,
event_value_in_usd,
COALESCE(total_item_quantity,
(SELECT SUM(CAST(IFNULL(i.quantity,0) AS INT64)) FROM UNNEST(items) AS i)
) AS resolved_item_quantity
FROM silver
)


SELECT
FORMAT_DATE('%Y-%m', PARSE_DATE('%Y%m%d', event_date)) AS month,
traffic_source_medium,
SUM(IFNULL(event_value_in_usd,0.0)) AS purchased_value_usd,
SUM(IFNULL(resolved_item_quantity,0)) AS total_items_purchased,
-- average items per purchase (safe divide by purchase_count)
SAFE_DIVIDE(SUM(IFNULL(resolved_item_quantity,0)), COUNT(1)) AS avg_items_per_purchase,
COUNT(1) AS purchase_count
FROM items_resolved
GROUP BY month, traffic_source_medium
ORDER BY month DESC, purchased_value_usd DESC