/*
 * Silver table: silver_purchase_traffic_source_medium
 *
 * - Filters purchase events
 * - Removes '(data deleted)' traffic sources
 * - Partitioned  by event_date_parsed
 * - incrementally load data everyday by matching unique  event_timestamp,user_pseudo_id,transaction_id columns
 * Columns:
 * - event_date_parsed: DATE - parsed event_date
 * - traffic_source_medium: STRING - medium field from GA4 traffic source
 * - user_pseudo_id: STRING - anonymous user id
 * - ga_session_id: INT64 - GA4 session id extracted from event_params
 * - transaction_id: STRING - purchase transaction id
 * - event_value_in_usd: FLOAT - value of purchase
 * - event_timestamp : INT64 - event timestamp in EPOCH format
 * - total_item_quantity : INT64 - total no items purchased
--below extra columns are kept for adhoc requests and debugging issues
 * - items : Records Repeated,
 * - traffic_source : Records Nullable
 */
config {
type: "incremental",
schema: "dataform",
name: "silver_purchase_traffic_source_medium",
description: "Cleaned purchases with traffic source medium. Partitioned by event_date. Incremental by event_date.",
uniqueKey: ["event_timestamp","user_pseudo_id","transaction_id"],
bigquery: {
partitionBy: "event_date_parsed",
clusterBy: ["traffic_source_medium","user_pseudo_id"]
}
}

SELECT
--parsing event_date to date type as we need monthly analytics
PARSE_DATE('%Y%m%d', event_date) AS event_date_parsed,
COALESCE(traffic_source.medium, '(unknown)') AS traffic_source_medium,
user_pseudo_id,
-- transaction id may be in ecommerce.transaction_id
IFNULL(ecommerce.transaction_id, CAST((SELECT value.string_value FROM UNNEST(event_params) WHERE key='transaction_id' LIMIT 1) AS STRING)) AS transaction_id,
-- ga_session_id: try event_params extractor, fall back to NULL
(SELECT
CASE
WHEN value.int_value IS NOT NULL THEN CAST(value.int_value AS INT64)
WHEN value.string_value IS NOT NULL AND value.string_value != '' THEN CAST(value.string_value AS INT64)
ELSE NULL
END
FROM UNNEST(event_params) WHERE key = 'ga_session_id' LIMIT 1) AS ga_session_id,
-- If event_value_in_usd is null, fallback to ecommerce.purchase_revenue_in_usd
COALESCE(event_value_in_usd, ecommerce.purchase_revenue_in_usd, 0.0) AS event_value_in_usd,
event_timestamp,
SAFE_CAST(ecommerce.total_item_quantity AS INT64) AS total_item_quantity,
items,
traffic_source
FROM `bigquery-public-data.ga4_obfuscated_sample_ecommerce.events_*`
WHERE event_name = 'purchase'
AND traffic_source.medium != '(data deleted)'
AND COALESCE(user_pseudo_id, '') != ''


